plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    compileJava.options.encoding('UTF-8')
    withJavadocJar()
    withSourcesJar()
}

group = 'com.github.romanqed'
archivesBaseName = 'dynamic-fsm'
version = System.getenv('RELEASE_VERSION') ?: '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.10.0'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.10.0'
    api group: 'org.ow2.asm', name: 'asm', version: '9.5'
    api group: 'org.ow2.asm', name: 'asm-commons', version: '9.5'
    api group: 'com.github.romanqed', name: 'jfunc', version: '1.0.0'
}

test {
    useJUnitPlatform()
}

jar {
    from sourceSets.main.output
    from sourceSets.main.allJava
}

signing {
    sign publishing.publications
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            // Specify artifacts
            artifactId = archivesBaseName
            groupId = group
            version = version
            from components.java
            // Configure pom
            pom {
                name.set('dynamic-fsm')
                description.set('A library that allows you to generate jvm byte code according ' +
                        'to the scheme of a finite automaton.')
                url.set('https://github.com/RomanQed/dynamic-fsm')
                organization {
                    name.set('com.github.romanqed')
                    url.set('https://github.com/RomanQed/')
                }
                issueManagement {
                    system.set('GitHub')
                    url.set('https://github.com/RomanQed/dynamic-fsm/issues')
                }
                licenses {
                    license {
                        name.set('Apache License 2.0')
                        url.set('https://github.com/RomanQed/dynamic-fsm/blob/main/LICENSE')
                    }
                }
                scm {
                    url.set('https://github.com/RomanQed/dynamic-fsm')
                    connection.set('scm:https://github.com/RomanQed/dynamic-fsm.git')
                    developerConnection.set('scm:https://github.com/RomanQed/dynamic-fsm.git')
                }
                developers {
                    developer {
                        id.set('RomanQed')
                        name.set('Roman Bakaldin')
                        email.set('gbakaldin@gmail.com')
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username System.getenv('SONATYPE_USERNAME')
                password System.getenv('SONATYPE_PASSWORD')
            }
        }
    }
}
